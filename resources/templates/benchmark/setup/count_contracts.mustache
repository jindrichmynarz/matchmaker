{{!
@param IRI source-graph
@param int min-main-object-count
@param int min-additional-object-count
}}

{{> templates/prefixes.mustache}}

SELECT (COUNT(?contract) AS ?count)
WHERE {
  {
    SELECT ?contract 
    WHERE {
      GRAPH <{{source-graph}}> {
        ?contract a pc:Contract ;
          pc:awardedTender/pc:bidder [] .
        OPTIONAL {
          ?contract pc:mainObject ?mainObject .
        }
        OPTIONAL {
          ?contract pc:additionalObject ?additionalObject .
        }
      }
    }
    GROUP BY ?contract
    HAVING (
            (COUNT(DISTINCT ?mainObject) >= {{min-main-object-count}})
            &&
            (COUNT(DISTINCT ?additionalObject) >= {{min-additional-object-count}})
            )
  }
}
