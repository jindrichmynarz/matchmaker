{{!
@param IRI   source-graph
@param IRI   sample-graph
@param IRI   zindex-graph
@param int   min-main-object-count
@param int   min-additional-object-count
@param float min-zindex
@param int   size
@param int   offset
}}

{{> templates/prefixes.mustache}}

INSERT {
  GRAPH <{{sample-graph}}> {
    ?contract a pc:Contract ;
      pc:awardedTender ?awardedTender .
    ?awardedTender pc:bidder ?bidder . 
  }
}
WHERE {
  {
    SELECT ?contract ?awardedTender ?bidder
    WHERE {
      GRAPH <{{source-graph}}> {
        ?contract a pc:Contract ;
          pc:contractingAuthority ?contractingAuthority ;
          pc:awardedTender ?awardedTender .
        ?awardedTender pc:bidder ?bidder .
        OPTIONAL {
          ?contract pc:mainObject ?mainObject .
        }
        OPTIONAL {
          ?contract pc:additionalObject ?additionalObject .
        }
      }
      {{#min-zindex}}
      GRAPH <{{source-graph}}> {
        ?contractingAuthority adms:identifier/skos:notation ?identifier .
      }
      GRAPH <{{zindex-graph}}> {
        [] pccz:zindex ?zindex ;
          rov:registration/skos:notation ?identifier .
        FILTER (?zindex >= {{min-zindex}})
      }
      {{/min-zindex}}
    }
    GROUP BY ?contract ?awardedTender ?bidder
    HAVING (
            (COUNT(DISTINCT ?mainObject) >= {{min-main-object-count}})
            &&
            (COUNT(DISTINCT ?additionalObject) >= {{min-additional-object-count}})
          )
    OFFSET {{offset}}
    LIMIT {{size}}
  }
}
