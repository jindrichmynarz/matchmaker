{{!
@param IRI business-entity
@param IRI source-graph
@param int limit
@param int additional-object-inhibition
}}

{{> templates/prefixes.mustache}}

SELECT ?match (SAMPLE(?_title) AS ?title) ?score
WHERE {
  {
    SELECT ?match (SUM(?outScore) AS ?score) 
    WHERE {
      {
        SELECT ?match ?cpvObject ((?inScore * xsd:decimal(?outScoreModifier)) AS ?outScore)
        WHERE {
          {
            {{! Get the CPV codes the business entity is most associated with. }}
            SELECT ?cpvObject (SUM(?objectPropertyInScore) AS ?inScore)
            WHERE {
              {
                {{! Virtuoso requires casting ?inScoreModifier to xsd:decimal. }}
                SELECT ?cpvObject ((COUNT(?contract) * xsd:decimal(?inScoreModifier)) AS ?objectPropertyInScore) 
                WHERE {
                  GRAPH <{{source-graph}}> {
                    {{> templates/matchmaker/sparql/business_entity/partials/bidder_property.mustache}}
                    {{> templates/matchmaker/sparql/business_entity/partials/tender_property.mustache}}
                    {{> templates/matchmaker/sparql/partials/object_property_inscore.mustache}}
                    ?contract ?tenderProperty [
                        ?bidderProperty <{{business-entity}}>
                      ] ;
                      ?objectProperty ?cpvObject .
                  }
                }
                GROUP BY ?cpvObject ?inScoreModifier
              }
            }
            GROUP BY ?cpvObject
          }
          GRAPH <{{source-graph}}> {
            {{> templates/matchmaker/sparql/business_entity/partials/bidder_property.mustache}}
            {{> templates/matchmaker/sparql/business_entity/partials/tender_property.mustache}}
            {{> templates/matchmaker/sparql/partials/object_property_outscore.mustache}}
            ?match ?objectProperty ?cpvObject .
            FILTER NOT EXISTS {
              ?match ?tenderProperty [
                  ?bidderProperty <{{business-entity}}> 
                ] .
            }
          }
        }
        GROUP BY ?match ?cpvObject ?inScore ?outScoreModifier
      }
    }
    GROUP BY ?match
    ORDER BY DESC(?score)
    LIMIT {{limit}}
  }
  OPTIONAL {
    GRAPH <{{source-graph}}> {
      ?match dcterms:title ?_title .
    }
  }
}
GROUP BY ?match ?score
