{{!
@param IRI contract
@param IRI matched-resource-graph
@param IRI source-graph
@param IRI cpv-frequencies-graph
@param map additional-object-inhibition
@param map main-object-inhibition
@param int limit
@param int offset (optional)
}}

{{> templates/prefixes.mustache}}

SELECT ?match (SAMPLE(?_label) AS ?label) ?score
WHERE {
  {
    SELECT ?match (SUM(?inScoreModifier * ?idfModifier * ?outScoreModifier * ?contractCount) AS ?score) 
    WHERE {
      {
        SELECT ?match ?inScoreModifier ?idfModifier ?outScoreModifier (COUNT(?contract) AS ?contractCount)
        WHERE {
          {
            SELECT ?cpvObject ?inScoreModifier ?idfModifier
            WHERE {
              GRAPH <{{matched-resource-graph}}> {
                {{> templates/matchmaker/sparql/partials/object_property_inscore.mustache}}
                <{{contract}}> ?objectProperty ?cpvObject .
              }
              GRAPH <{{cpv-frequencies-graph}}> {
                ?cpvObject ex:frequency ?idfModifier .
              }
            }
          }
          GRAPH <{{source-graph}}> {
            {{> templates/matchmaker/sparql/partials/object_property_outscore.mustache}}
            {
              ?contract pc:awardedTender [
                  pc:bidder ?match
                ] ;
                ?objectProperty ?cpvObject .
            } UNION {
              {{> templates/matchmaker/sparql/partials/business_entity_classes.mustache}}
              ?match a ?businessEntityClass ;
                ?objectProperty ?cpvObject .
            }
          }
        }
        GROUP BY ?match ?inScoreModifier ?idfModifier ?outScoreModifier
      }
    }
    GROUP BY ?match
    ORDER BY DESC(?score)
    LIMIT {{limit}}
    {{#offset}}
    OFFSET {{offset}}
    {{/offset}}
  }
  GRAPH <{{source-graph}}> {
    {{> templates/matchmaker/sparql/partials/business_entity_legal_names.mustache}}
    OPTIONAL {
      ?match ?businessEntityLegalName ?_label .
    }
  }
}
GROUP BY ?match ?score
