{{!
@param IRI contract
@param IRI source-graph
@param int limit
@param int additional-object-inhibition
}}

{{> templates/prefixes.mustache}}

CONSTRUCT {
  ?businessEntity gr:legalName ?legalName ;
    vrank:hasRank [
        vrank:rankValue ?score 
      ] .
}
WHERE {
  {
    SELECT ?businessEntity (SAMPLE(?_legalName) AS ?legalName) ?score
    WHERE {
      {
        SELECT ?businessEntity (SUM(?inScoreModifier * ?outScoreModifier * ?contractCount) AS ?score) 
        WHERE {
          {
            SELECT ?businessEntity ?inScoreModifier ?outScoreModifier (COUNT(DISTINCT ?contract) AS ?contractCount)
            WHERE {
              {
                SELECT ?cpvObject ?inScoreModifier 
                WHERE {
                  GRAPH <{{source-graph}}> {
                    VALUES (?objectProperty     ?inScoreModifier) {
                          (pc:mainObject       1)
                          (pc:additionalObject {{additional-object-inhibition}})    
                    }
                    <{{contract}}> ?objectProperty ?cpvObject .
                  }
                }
              }
              GRAPH <{{source-graph}}> {
                VALUES (?objectProperty     ?outScoreModifier) {
                      (pc:mainObject       1)
                      (pc:additionalObject {{additional-object-inhibition}})
                }
                ?contract pc:awardedTender [
                    pc:supplier ?businessEntity
                  ] ;
                  ?objectProperty ?cpvObject .
              }
            }
            GROUP BY ?businessEntity ?inScoreModifier ?outScoreModifier
          }
        }
        GROUP BY ?businessEntity
        ORDER BY DESC(?score)
        LIMIT {{limit}}
      }
      GRAPH <{{source-graph}}> {
        ?businessEntity gr:legalName ?_legalName .
      }
    }
    GROUP BY ?businessEntity ?score
  }
}
